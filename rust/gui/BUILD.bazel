load("@rules_rust//rust:defs.bzl", "rust_binary", "rust_library")
load("@crate_index//:defs.bzl", "all_crate_deps")
load("@bazel_skylib//rules:common_settings.bzl", "bool_flag")



bool_flag(
    name = "use_sdl",
    build_setting_default = False,
)

config_setting(
    name = "display_driver_sdl",
    flag_values = {":use_sdl": "true"},
)

gui_features = select({
    ":display_driver_sdl": ["sdl2"],
    "//conditions:default": [],
})

gui_optional_deps = select({
    ":display_driver_sdl": [
        "@crate_index//:sdl2",
        "@crate_index//:astc-decode",
    ],
    "//conditions:default": [],
})


rust_library(
    name = "gui",
    srcs = [
        "src/lib.rs",
    ],
    crate_features = gui_features,
    deps = all_crate_deps() + gui_optional_deps,
)


rust_binary(
    name = "gui_bin",
    srcs = ["src/bin/main.rs"],
    crate_features = gui_features,
    deps = all_crate_deps() + [":gui"],
)

